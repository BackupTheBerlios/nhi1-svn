#+
#§  \file       theLink/tests/int.test
#§  \brief      \$Id$
#§  
#§  (C) 2002 - NHI - #1 - Project - Group
#§  
#§  \version    \$Rev$
#§  \author     EMail: aotto1968 at users.berlios.de
#§  \attention  this software has GPL permissions to copy
#§              please contact AUTHORS for additional information
#§

if {[info exists env(TS_LIBRARY)]} {
  source $env(TS_LIBRARY)
} else {
  source [file join [file dirname [info script]] library.tcl]
}

##
## -----------------------------------------------------------------------
## init
##

#set tcl_traceExec 2

set MAX_INT    [tclmsgque const maxI]
set MIN_INT    [tclmsgque const minI]

##
## -----------------------------------------------------------------------
## Master/Child + Binary yes/no
##

# 1,2, ... tclmsgque (parent/child)
 foreach I [getNum 3] {

 # transport-type: string or binary
  foreach B [getEnv BIN_LST] {

  # socket-type: pipe, uds or tcp
   foreach C [getEnv COM_LST] {

    # server-type: server server.tcl
    foreach S [filter SRV_LST $C] {

##
## -----------------------------------------------------------------------
## int
##

# startup binary --------------------------
Setup $I $B $C $S

# tests ----------------------------

test int-1-($I|$B|$C|$S) {echo datatype (MQ_INT)} {
    tclmsgque print objtype [Echo1 $FH_LAST ECOU ReadI SendI $MAX_INT]
} "int"

test int-2-($I|$B|$C|$S) {(MQ_WID to MQ_INT) fit max} \
    -body           {Echo1 $FH_LAST ECOI ReadI SendI $MAX_INT} \
    -returnCodes    ok \
    -result         $MAX_INT
 
test int-3-($I|$B|$C|$S) {(MQ_WID to MQ_INT) fit min} \
    -body           {Echo1 $FH_LAST ECOI ReadI SendI $MIN_INT} \
    -returnCodes    ok \
    -result         $MIN_INT

test int-4-($I|$B|$C|$S) {(MQ_STR to MQ_INT) fit max} \
    -body           {Echo1 $FH_LAST ECOI ReadI SendC $MAX_INT} \
    -returnCodes    ok \
    -result         $MAX_INT

test int-5-($I|$B|$C|$S) {(MQ_STR to MQ_INT) fit min} \
    -body           {Echo1 $FH_LAST ECOI ReadI SendC $MIN_INT} \
    -returnCodes    ok \
    -result         $MIN_INT

test int-6-($I|$B|$C|$S) {(MQ_STR to MQ_INT) error size} \
    -body           {Echo1 $FH_LAST ECOI ReadI SendC 1234.7} \
    -returnCodes    error \
    -match          glob \
    -result         {[cC]> (client-0) \[service-call-error\] <Tok|ECOI> <Num|1>*
[sS]> (server-0) \[*pBufferGetA4\] size ERROR, can not convert '1234.7' into an 'INT' object*}

test int-7-($I|$B|$C|$S) {range error underflow} \
    -body           {Echo1 $FH_LAST ECOI ReadI SendC ${MIN_INT}0} \
    -returnCodes    error \
    -match          glob \
    -result         {[cC]> (client-0) \[service-call-error\] <Tok|ECOI> <Num|1>*
[sS]> (server-0) \[*pBufferGetA4\] underflow ERROR, can not convert '*' into an 'INT' object*}

test int-8-($I|$B|$C|$S) {range error overflow} \
    -body           {Echo1 $FH_LAST ECOI ReadI SendC ${MAX_INT}0} \
    -returnCodes    error \
    -match          glob \
    -result         {[cC]> (client-0) \[service-call-error\] <Tok|ECOI> <Num|1>*
[sS]> (server-0) \[*pBufferGetA4\] overflow ERROR, can not convert '*' into an 'INT' object*}
 
if {$B eq "binary"} {
    test int-9-($I|$B|$C|$S) {(MQ_WID to MQ_INT) error WID type} \
	-body           {Echo1 $FH_LAST ECOI ReadI SendW 1234} \
	-returnCodes    error \
	-match          glob \
	-result         {[cC]> (client-0) \[service-call-error\] <Tok|ECOI> <Num|1>*
[sS]> (server-0) \[*pBufferGetA4\] type ERROR, it is not allowed to convert an 'WID' object into an 'INT' object*}
} else {
    test int-9-($I|$B|$C|$S) {(MQ_WID to MQ_INT) error INT type} \
	-body           {Echo1 $FH_LAST ECOI ReadI SendW 1234} \
	-returnCodes    ok \
	-result         1234
}

if {$B eq "binary"} { 
    test int-10-($I|$B|$C|$S) {(MQ_DBL to MQ_INT) error DBL type} \
	-body           {Echo1 $FH_LAST ECOI ReadI SendD 1234.5} \
	-returnCodes    error \
	-match          glob \
	-result         {[cC]> (client-0) \[service-call-error\] <Tok|ECOI> <Num|1>*
[sS]> (server-0) \[*pBufferGetA4\] type ERROR, it is not allowed to convert an 'DBL' object into an 'INT' object*}
} else {
    test int-10-($I|$B|$C|$S) {(MQ_DBL to MQ_INT) error DBL type} \
	-body           {Echo1 $FH_LAST ECOI ReadI SendD 1234.5} \
	-returnCodes    error \
	-match          glob \
	-result         {[cC]> (client-0) \[service-call-error\] <Tok|ECOI> <Num|1>*
[sS]> (server-0) \[*pBufferGetA4\] size ERROR, can not convert '1.2345*e+*03' into an 'INT' object*}
}
 
test int-11-($I|$B|$C|$S) {(MQ_LST to MQ_INT) error LST type} \
    -body           {
      $FH_LAST SendSTART
      $FH_LAST SendL_START
      $FH_LAST SendD 1234.5
      $FH_LAST SendL_END
      $FH_LAST SendEND_AND_WAIT ECOI
      $FH_LAST ReadI
    } \
    -returnCodes    error \
    -match          glob \
    -result         {[cC]> (client-0) \[service-call-error\] <Tok|ECOI> <Num|1>*
[sS]> (server-0) \[*pBufferGetA4\] type ERROR, it is not allowed to convert an 'LST' object into an 'INT' object*}

test int-12-($I|$B|$C|$S) {set/get context data} \
    -body           {
	for {set i 0} {$i < $I} {incr i} {
	  set ctx $FH(0-$i)
	  $ctx SendSTART
	  $ctx SendI $i
	  $ctx SendEND SETU
	}
	for {set i 0} {$i < $I} {incr i} {
	  set ctx $FH(0-$i)
	  $ctx SendSTART
	  $ctx SendEND_AND_WAIT GETU
	  set ret [$ctx ReadI]
	  if {$i != $ret} {
	    error "expect value '$i' but got '$ret'"
	  }
	}
	return OK
    } \
    -result	{OK}

## !ACHTUNG! dieser test muss am ende stehen
test int-XX-($I|$B|$C|$S) {no data} \
    -body           {
      $FH_LAST SendSTART
      $FH_LAST SendEND_AND_WAIT ECOI
      $FH_LAST ReadI
    } \
    -returnCodes    error \
    -match          glob \
    -result         {[cC]> (client-0) \[service-call-error\] <Tok|ECOI> <Num|213>*
[sS]> (server-0) \[*sReadA4\] request more arguments than available*}

# cleanup ---------------------------

Cleanup

##  
## -----------------------------------------------------------------------
## finish loop
##
   
    }; # SRV_LST
   }; # COM_LST
  }; # BIN_LST
 }; # Master/Child  
unset -nocomplain I B C S

##
## -----------------------------------------------------------------------
## finish
##
freeTests
